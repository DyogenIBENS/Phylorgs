"""Pipeline to realign an ensembl alignment and run treebest to get a new reconciliation
Steps:

- extract the sequences from the existing alignment: `_cdna.fa`
- shorten names: `_cdna.short.fa`
- translate:     `_prot.fa`
- t-coffee:      `_protaln.fa`, `_newtree.nwk`
- backtrans:     `_backtrans.mfa`
- specify:       `_backtrans-sp.mfa`
- treebest:      `_best.nwk`
- treebest2genomicus: `_best_fulltree.nwk`
- codeml tree fmt:    `_best_codeml.nwk`

"""

SPTREENAME = config.setdefault('sptreename', 'timetree.taxadots')

rule al2seq:
	input: "{genefam}_genes.fa"
	output: "{outdir}/{genefam}_cdna.fa"
	shell:
		"sed '/^[^>]/s/-//g' {input} > {output}"

rule shorten:
	input: rules.al2seq.output
	output: "{outdir}/{genefam}_cdna.short.fa"
	shell:
		r"sed -r 's/^(>\w+).*$/\1/' {input} > {output}"

rule translate:
	input: rules.shorten.output
	output: "{outdir}/{genefam}_prot.fa"
	shell:
		"~glouvel/scripts/seqtools/fasta_translate.py {input} {output}"
		#"~glouvel/scripts/evosite3d/translate_dna.py {input} {output}"

rule align:
	input: rules.translate.output
	output:
		aln="{outdir}/tcoffee/{genefam}_protaln.fa",
		tree="{outdir}/tcoffee/{genefam}_newtree.nwk"
	shell:
		"t_coffee {input} -type=PROTEIN -method mafftgins_msa, muscle_msa, kalign_msa -mode=mcoffee -output=fasta_aln -outfile={output.aln} -newtree={output.tree}"

rule backtrans:
	input:
		aa_aln=rules.align.output.aln,
		nt_seq=rules.shorten.output
	output: "{outdir}/treebest/{genefam}_backtrans.mfa"
	shell:
		"treebest backtrans {input.aa_aln} {input.nt_seq} > {output}"

rule specify:
	input: rules.backtrans.output
	output: "{outdir}/treebest/{genefam}_backtrans-sp.mfa"
	shell:
		"~glouvel/scripts/seqtools/specify.py {input} {output}"

rule treebest:
	input:
		aln=rules.specify.output,
		sp="{{genefam}}_{0}.nwk".format(SPTREENAME)
		#sp="{genefam}_timetree.taxadots.nwk"
	output: "{outdir}/treebest/{genefam}_best.nwk"
	shell:
		"treebest best -f {input.sp} {input.aln} -o {output}"

rule treebest2genomicus:
	input: rules.treebest.output
	output: "{outdir}/treebest/{genefam}_best_fulltree.nwk"
	shell:
		"~glouvel/scripts/treetools/treebest2genomicus.py {input} '' {output}"

rule reshape4codeml:
	input: rules.treebest2genomicus.output
	output: "{outdir}/treebest/{genefam}_best_codeml.nwk"
	shell:
		"~glouvel/scripts/codeml/reshape_tree.py {input} {output}"

